group 'com.frostwire'
version '1.1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    mavenCentral()
}

dependencies {
}

jar {
    archiveName = 'jlibtorrent-' + project.version + '.jar'

    manifest {
        attributes 'Main-Class': 'com.frostwire.jlibtorrent.tools.Main'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task nativeAndroidJar(type: Zip) {
    destinationDir file("$buildDir/libs")
    baseName 'jlibtorrent-android'
    extension 'jar'
    from fileTree(dir: 'binaries/android', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeAndroidJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId 'jlibtorrent'
            version project.version

            from components.java

            artifact sourceJar {
                classifier 'sources'
            }
        }
        nativeAndroid(MavenPublication) {
            groupId project.group
            artifactId 'jlibtorrent-android'
            version project.version

            artifact nativeAndroidJar
        }
    }

    repositories {
        maven {
            url 'repo'
        }
    }
}
